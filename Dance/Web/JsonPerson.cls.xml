<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Dance.Web.JsonPerson">
<Super>Dance.Web.JsonBase</Super>
<TimeCreated>63294,86309.969149</TimeCreated>

<Method name="ConvertToProxyObject">
<Description>
Convert "Person" object to %ZEN.proxyObject (for JSON)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>object:Dance.Data.Person,convertParams:%ZEN.proxyObject=""</FormalSpec>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	
	try{
		set proxy.id = object.%Id()
		set proxy.lastName = object.LastName.Value
		set proxy.firstName = object.FirstName.Value
		set proxy.gender = object.Gender
		
		if (object.DOB '= "") set proxy.dob = $ZDATE(object.DOB, 3)
		
		set proxy.numberUDSR = object.NumberUDSR
		set proxy.numberWDSF = object.NumberWDSF
		
		set proxy.country = ##class(JsonCountry).ConvertToProxyObject(object.Country)
		
		set proxy.laClass = ##class(JsonDancerClass).ConvertToProxyObject(object.LAClass)
		set proxy.stClass = ##class(JsonDancerClass).ConvertToProxyObject(object.STClass)
	}
	catch(ex){
		set proxy = ##class(%ZEN.proxyObject).%New()
	}
	
	quit proxy
]]></Implementation>
</Method>

<Method name="WriteStatisticsToDevice">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	set proxy.allCount = 0
	set proxy.udsrCount = 0
	set proxy.wdsfCount = 0
	set proxy.otherCount = 0
	
	&sql(SELECT COUNT(ID) INTO :proxy.allCount FROM Dance_Data.Person)
	&sql(SELECT COUNT(ID) INTO :proxy.udsrCount FROM Dance_Data.Person WHERE NumberUDSR IS NOT NULL)
	&sql(SELECT COUNT(ID) INTO :proxy.wdsfCount FROM Dance_Data.Person WHERE NumberWDSF IS NOT NULL)
	&sql(SELECT COUNT(ID) INTO :proxy.otherCount FROM Dance_Data.Person WHERE NumberUDSR IS NULL AND NumberWDSF IS NULL)
	
	set proxy.udsrLastUpdated = $G(^Settings("Dance","UDSRPersonsLastUpdated"))
	
	do proxy.%ToJSON()
	
	quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
