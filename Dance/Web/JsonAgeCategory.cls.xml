<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Dance.Web.JsonAgeCategory">
<Super>Dance.Web.JsonBase</Super>
<TimeCreated>63290,68235.937948</TimeCreated>

<Method name="ConvertToProxyObject">
<Description>
Convert "AgeCategory" object to %ZEN.proxyObject (for JSON)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>object:Dance.Data.AgeCategory,isFullInfo:%Boolean=0</FormalSpec>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	
	try{
		set proxy.id = object.%Id()
		set proxy.name = object.Name.Value
		set proxy.code = object.Code
		set proxy.maxAge = object.MaxAge
		set proxy.minAge = object.MinAge
	}
	catch(ex){
		set proxy = ##class(%ZEN.proxyObject).%New()
	}
	
	quit proxy
]]></Implementation>
</Method>

<Method name="WriteAllToDevice">
<Description>
Write to device "children" array of proxyObjects </Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	set proxy.children = ##class(%ListOfObjects).%New()
	
	&sql(DECLARE Cur1 CURSOR FOR 
		 	SELECT ID
		 	FROM Dance_Data.AgeCategory
		 	ORDER BY Name->Value)	
		
	&sql(OPEN Cur1)
	for  
	{	
		&sql(FETCH Cur1 INTO :id) 
		quit:(SQLCODE '= 0)
		
		do proxy.children.Insert(..ConvertToProxyObject(##class(Dance.Data.AgeCategory).%OpenId(id)))	
	}
	
	&sql(CLOSE Cur1)
	
	do proxy.%ToJSON()
	
	quit $$$OK
]]></Implementation>
</Method>

<Method name="WriteAllForGridToDevice">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK
	
	try{
		$$$THROWONERROR(st, ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(%request.Content,,.params, 1))
      	
      	set whereCondition = ""
      	
      	if (params.other.status = "1"){
	      	set whereCondition = "IsActive=1"
      	}
      	elseif (params.other.status = "0"){
      		set whereCondition = "IsActive=0"
      	}
      	
      	$$$THROWONERROR(st, ..WriteJsonForGrid("SELECT ID FROM Dance_Data.AgeCategory", "Dance.Data.AgeCategory", params, whereCondition))
	}
	catch ex {
		set st = ex.AsStatus()
	}

	quit st
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
</Storage>
</Class>
</Export>
