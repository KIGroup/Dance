<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Dance.Web.JsonParticipant">
<Super>Dance.Web.JsonBase</Super>
<TimeCreated>63295,5829.157358</TimeCreated>

<Method name="ConvertToProxyObject">
<Description>
Convert "Participant" object to %ZEN.proxyObject (for JSON)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>object:Dance.Data.Participant,isFullInfo:%Boolean=0</FormalSpec>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	
	try{
		set proxy.id = object.%Id()
		set proxy.couple = ##class(JsonCouple).ConvertToProxyObject(object.Couple)
		set proxy.paymentStatus = object.PaymentStatus
		set proxy.coupleCompetitionsCount = object.CoupleCompetitionsCount
		
		if (isFullInfo){
			set proxy.competition = ##class(JsonCompetition).ConvertToProxyObject(object.Competition)
		}
	}
	catch(ex){
		set proxy = ##class(%ZEN.proxyObject).%New()
	}
	
	quit proxy
]]></Implementation>
</Method>

<Method name="Create">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK
	
	try{
		TSTART
		
		$$$THROWONERROR(st, ##class(Dance.Data.Tournament).CheckUpdatePrivilege())
		
		$$$THROWONERROR(st, ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject($ZCVT(%request.Content.Read(),"I", "UTF8"),,.data, 1))
      	
      	
 		set competition = ##class(Dance.Data.Competition).%OpenId(data.competitionId)
 		set prt = ##class(Dance.Data.Participant).%New()
 		
 		set prt.Competition = competition
 		
 		set prt.Man = ##class(Dance.Data.Person).GetByNumberUSDR(data.number)
 		if ($ISOBJECT(prt.Man) = 0){
 			set prt.Man = ##class(Dance.Data.Person).GetByNumberWDSF(data.number)
 		}
 		
 		set prt.Woman = ##class(Dance.Data.Person).GetByNumberUSDR(data.number)
 		if ($ISOBJECT(prt.Woman) = 0){
 			set prt.Woman = ##class(Dance.Data.Person).GetByNumberWDSF(data.number)
 		}
      	
      	
      	
      	set st = prt.%Save()
      	if $$$ISERR(st) $$$ThrowStatus(##class(Dance.Utils.Msg).GetErrorStatus("AddParticipantError"," | "_st))
      	
      	TCOMMIT
	}
	catch ex {
		TROLLBACK
		set st = ex.AsStatus()
	}
			
	quit st
]]></Implementation>
</Method>

<Method name="WriteAllForGridToDevice">
<Description>
Write objects to device, page by page for grid.</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK
	
	try{
		$$$THROWONERROR(st, ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(%request.Content,,.params, 1))
 		
 		set whereCondition = "Competition = '"_..ParseParameter(params.other.competitionId)_"'"     	
 		
      	$$$THROWONERROR(st, ..WriteJsonForGrid("SELECT ID FROM Dance_Data.Participant", "Dance.Data.Participant", params, whereCondition))
	}
	catch ex {
		set st = ex.AsStatus()
	}

	quit st
]]></Implementation>
</Method>

<Method name="WriteAllForCoupleToDevice">
<ClassMethod>1</ClassMethod>
<FormalSpec>tournamentId:%String,coupleId:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK
	set proxy = ##class(%ZEN.proxyObject).%New()
	set proxy.children = ##class(%ListOfObjects).%New()
	
	try{
		&sql(DECLARE PrtCur CURSOR FOR
			 SELECT ID 
			 FROM Dance_Data.Participant
			 WHERE Couple = :coupleId AND Competition->Tournament = :tournamentId)	
		
		&sql(OPEN PrtCur)
		for  
		{	
			&sql(FETCH PrtCur INTO :id) 
			quit:(SQLCODE '= 0)
	
			set prtProxy = ..ConvertToProxyObject(##class(Dance.Data.Participant).%OpenId(id), $$$YES)
			do proxy.children.Insert(prtProxy)
		}
		
		&sql(CLOSE PrtCur)
		
		do proxy.%ToJSON()
	}
	catch ex{
		set st = ex.AsStatus()	
	}
	
	q st
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// Dance.Web.JsonParticipant:WriteAllForTournamentForGridToDevice

]]></Content>
</UDLText>

<Method name="WriteAllForTournamentForGridToDevice">
<Description>
Write all objects for tournament to device, page by page for grid.</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK
	
	try{
		$$$THROWONERROR(st, ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(%request.Content,,.params, 1))
 		
 		set whereCondition = "Competition->Tournament = '"_..ParseParameter(params.other.tournamentId)_"'"     	
 		set groupCondition = "GROUP BY Couple"
 		
      	$$$THROWONERROR(st, ..WriteJsonForGrid("SELECT ID FROM Dance_Data.Participant", "Dance.Data.Participant", params, whereCondition, groupCondition))
	}
	catch ex {
		set st = ex.AsStatus()
	}

	quit st
]]></Implementation>
</Method>
</Class>
</Export>
